plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}

group = 'com.testtask'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'


sourceSets {
	integrationTest {
		java.srcDir 'src/integration-test/java'
		resources.srcDir 'src/integration-test/resources'

		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
	testImplementation.extendsFrom optionalImplementation
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
}

check.dependsOn integrationTest


repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-ui:1.3.0'
	implementation 'net.spy:spymemcached:2.12.3'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	integrationTestImplementation 'org.testcontainers:testcontainers:1.13.0'
	//integrationTestImplementation 'org.testcontainers:junit-jupiter:1.13.0'
}

test {
	useJUnitPlatform()
}

